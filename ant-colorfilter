#!/usr/bin/awk -f
# [n];km	color n, style k
# style	0	reset
# 	1	bold
# 	2	normal
# 	3	normal
#	4	underlined
#	5	blink
# 	6	normal
#	7	inverted
#	22	bold off
# color 30	black	bright: grey
#	31	red
#	32	green
#	33	orange	bright: yellow
#	34	blue
#	35	magenta
#	36	cyan
#	37	gray	bright: white
#	39	default
# backg	40+x	Background color, as above
function colorize_inner(text)
{
	return colorize_good(colorize_warnings(colorize_errors(colorize_files(colorize_javaerrors(text)))));
}
function colorize_files(text,     parts)
{
	old_ic = IGNORECASE
	IGNORECASE = 1
	if (0 != match(text, /^(.*[[:space:]:])?([^[:space:]:]+):([0-9,+-]+):(.*)$/, parts) && 0 == match(parts[2], /^[0-9]+$/)) { # file:123: Stuff. With file not only composed of digits (prevent time like 13:37:42,314 from matching)
		IGNORECASE = old_ic
		return colorize_inner(parts[1]) "\x1B[35m" parts[2] "\x1B[36m" ":" "\x1B[32m" parts[3] "\x1B[36m" ":" "\x1B[0m" colorize_inner(parts[4]);
	} else {
		IGNORECASE = old_ic
		return text;
	}
}
function colorize_javaerrors(text,     parts)
{
	if (text == "Caused by: ") {
		return "\x1B[35;1m" text "\x1B[0m";
	} else if (0 != match(text, /(.*)Exception in thread "([^"]+)"(\s.*)/, parts)) {
		return colorize_inner(parts[1]) "\x1B[35;1m" "Exception in thread \"" "\x1B[36;22m" parts[2] "\x1B[35;1m" "\"" "\x1B[0m" colorize_javaerrors(parts[3])
	#                                 at     com.package.             Classname           $Innerclass                   .function                 ( file   :line    )
	#                            11112222223455555555555555555555555546666666666666666666677777777777777777777777777768888888888888888888888888649990000000911111111999999999999992222
	} else if (0 != match(text, /(.*)(at\s)((([a-z_][a-z0-9_]*\.){1,})([A-Z_][A-Za-z0-9_]*(\$[0-9A-Z_][A-Za-z0-9_]*)?)(\.[<a-z_][<>a-zA-Z0-9_]*))(\(([^:]+):([0-9]+)\)|\([^\)]*\))(.*)/, parts)) {
		fileref = parts[9]
		if (parts[10] != "") {
			fileref = "(" "\x1B[35m" parts[10] "\x1B[36m" ":" "\x1B[32m" parts[11] "\x1B[36;6m" ")"
		}
		return colorize_inner(parts[1]) "\x1B[35;1m" "at " "\x1B[34;1m" parts[4] "\x1B[31;1m" parts[6] "\x1B[36;1m" parts[8] "\x1B[36;22m" fileref colorize_inner(parts[12])
	#                                  com.package.              Classname           $Innerclass                Exception                   : Optional msg
	#                            1111112333333333333333333332222244444444444444444444555555555555555555555555555666666666666666666666666666666666664447777
	} else if (0 != match(text, /(.*\s)(([a-z_][a-z0-9_]*\.){1,})([A-Z_][A-Za-z0-9_]*(\$[0-9A-Z_][A-Za-z0-9_]*)?(Exception|Error|Throwable|Failure)\y)(.*)/, parts)) {
		return colorize_inner(parts[1]) "\x1B[31;1m" parts[2] "\x1B[31m" parts[4] "\x1B[37;1m" colorize_inner(parts[7])
	} else {
		return text;
	}
}
function colorize_errors(text,     parts)
{
	old_ic = IGNORECASE
	IGNORECASE = 1
	if (0 != match(text, /(.*)((error|problem|fail|failed|failure|exception)s?)(.*)/, parts)) {
		IGNORECASE = old_ic
		return colorize_inner(parts[1]) "\x1B[31;1m" parts[2] "\x1B[0m" colorize_inner(parts[4]);
	} else {
		IGNORECASE = old_ic
		return text;
	}
}
function colorize_warnings(text,     parts)
{
	old_ic = IGNORECASE
	IGNORECASE = 1
	if (0 != match(text, /(.*)(warnings?|not found|unresolved|missing)(.*)/, parts)) {
		IGNORECASE = old_ic
		return colorize_inner(parts[1]) "\x1B[33m" parts[2] "\x1B[0m" colorize_inner(parts[3]);
	} else {
		IGNORECASE = old_ic
		return text;
	}
}
function colorize_good(text,     parts)
{
	old_ic = IGNORECASE
	IGNORECASE = 1
	if (0 != match(text, /(.*)\y(okay|ok|good|successful|success)\y(.*)/, parts)) {
		IGNORECASE = old_ic
		return colorize_inner(parts[1]) "\x1B[32m" parts[2] "\x1B[0m" colorize_inner(parts[3]);
	} else {
		IGNORECASE = old_ic
		return text;
	}
}
function coloring_log4j(text,     parts)
{
	old_ic = IGNORECASE
	IGNORECASE = 1
	#                      TIME                                                    ,.secons        [thread]         INFO         Class    -       Blah
	#                      111111111111111111111111111111111111111111111111111111112222222221133333444444444444555556666666677777888888889999999990000
	if (0 != match(text, /^([0-9]{4}-[0-9]{2}-[0-9]{2}\s+[0-9]{2}:[0-9]{2}:[0-9]{2}(,[0-9]*)?)(\s+)(\[[^\]]+\])(\s+)([^[:space:]]+)(\s+)([^[:space:]]*)(\s*-\s*)(.*)$/, parts)) {
		IGNORECASE = old_ic
		tagcolor = "\x1B[35;1m"
		if (parts[6] == "TRACE") {
			tagcolor = "\x1B[30;1m";
		} else if (parts[6] == "DEBUG") {
			tagcolor = "\x1B[34;1m";
		} else if (parts[6] == "INFO") {
			tagcolor = "\x1B[32;1m";
		} else if (parts[6] == "WARN") {
			tagcolor = "\x1B[33;1m";
		} else if (parts[6] == "ERROR") {
			tagcolor = "\x1B[31;1m"
		} else if (parts[6] == "FATAL") {
			tagcolor = "\x1B[41;37;1m"
		}
		return "\x1B[30;1m" parts[1] "\x1B[0m" parts[3] "\x1B[36m" parts[4] "\x1B[0m" parts[5] tagcolor parts[6] "\x1B[0m" parts[7] "\x1B[34;1m" parts[8] "\x1B[0m" parts[9] colorize_inner(parts[10]);
	} else {
		IGNORECASE = old_ic
		return colorize_inner(text);
	}
}
{
	if (0 != match($0, /^[a-zA-Z_-]+:$/, parts)) { # ^target:$
		printf "\x1B[37;1m%s", $0;
	} else if (0 != match($0, /^(\s*)(\[java\])(\s*)(.*)$/, parts)) { # ^     [java] Log4j stuff
		printf "%s\x1B[36m%s\x1B[0m%s%s", parts[1], parts[2], parts[3], coloring_log4j(parts[4])
	} else if (0 != match($0, /^(\s*)(\[[^\]]+\])(\s.*)$/, parts)) { # ^   [command]  Stuff$
		printf "%s\x1B[36m%s\x1B[0m%s", parts[1], parts[2], colorize_inner(parts[3]);
	} else if (0 != match($0, /BUILD SUCCESSFUL/, parts)) { # BUILD SUCCESSFUL
		printf "\x1B[32;1m%s", parts[0];
	} else if (0 != match($0, /BUILD FAILED/, parts)) { # BUILD FAILED
		printf "\x1B[31;1m%s", parts[0];
	} else {
		printf "%s", colorize_inner($0);
	}
	print "\x1B[0m";
}
